<!DOCTYPE HTML>

<html>
    <head>
        <meta name="viewport" value="width=device-width, initial-scale=1.0, user-scalable=yes">
        <meta name="description" value="# Telegram Bot and Something (WIP)

One of my friend asked for a help.
Though this time from a different person and on different problem.
We are going to build a bot for Telegram instant messenger ser">
        <meta name="author" value="Ibnu D. Aji">
        <link rel="stylesheet" type="text/css" href="static/css/milligram.min.css">
        <title>
            Nothing Unusual - Telegram Bot and Something
        </title>
    </head>
    <body>
        <main class="wrapper">
            <nav class="navigation">
                <section class="container">
                    <a class="navigation-title" href="index.html">
                        Home
                    </a>
                    <ul class="navigation-list float-right">
                        <li class="navigation-item">
                            <a href="2018-01-01-about.html" class="navigation-link">
                                About
                            </a>
                        </li>
                    </ul>
                </section>
            </nav>
            <section class="container">
                <h1>Telegram Bot and Something (WIP)</h1><p>One of my friend asked for a help.
Though this time from a different person and on different problem.
We are going to build a bot for Telegram instant messenger service which talks to database.
For the sake of giving an example, let's say that we will create a telegram bot which
records the income and expense.
Words of caution: trust me, it's really hairy and ugly.</p><p>Okay, let's start the mess.</p><h3>Preparation</h3><p>Just type <code>stack new OurBot new-template</code> into your terminal and then modify <code>stack.yaml</code>.
Insert the following lines in it</p><pre><code># stack.yaml
packages:
- .
- location:
    git: https://github.com/fizruk/telegram-bot-simple
    commit: c1cc6bbba14ca79c897586e2de28433193c0b9fd
  extra-dep: true
</code></pre><p>Which means that we will use that git repo (on that specified commit) on our program.
As for the reason, I failed to compile <a href="https://hackage.haskell.org/package/telegram-api"><code>telegram-api</code></a>
in the server I'm deploying to.</p><p>And then we will modify <code>package.yaml</code> to include the repo above as one of our dependencies.</p><pre><code># package.yaml
dependencies:
- base &gt;= 4.7 &amp;&amp; &lt; 5
- telegram-bot-simple
</code></pre><p>Where <code>telegram-bot-simple</code> is the name of the library which we previously included
in our <code>stack.yaml</code>.</p><p>Let's build it for a giggle by issuing <code>stack build</code> at the project's root directory.
It should compile just fine, I guess.</p><p>Next, we will create our domain model.</p><h3>Database Thingy</h3><p>We will add a few dependencies in our <code>package.yaml</code>.</p><ul><li><code>persistent</code> for talking to database.</li><li><code>persistent-postgresql</code> for talking to postgresql, specifically.</li><li><code>persistent-template</code>, it eases our life a bit. Though prolongs the compile time.</li><li><code>esqueleto</code> it's nice to join the tables in a <del>bar</del> database.</li><li><code>text</code>, I just like <code>Text</code>.</li><li><code>time</code></li></ul><p>Then, we will create a file named <code>Model.hs</code> in our <code>src</code> directory and include it
as one of our source files.</p><p>To simplify a bit, we will create two tables named <code>income</code> and <code>expense</code>.
The <code>income</code> table will have <code>source</code> (<code>Text</code>), <code>amount</code> (<code>Double</code>), and <code>when</code> (<code>UTCTime</code>) while <code>expense</code>
table will have <code>towhom</code> (<code>Text</code>), <code>amount</code> (<code>Double</code>), and <code>when</code> (<code>UTCTime</code>).</p><p>In order to accomplish that, we will edit <code>Model.hs</code> as the following</p><pre><code>-- Model.hs
{-# LANGUAGE ExistentialQuantification #-}
{-# LANGUAGE GADTs #-}
{-# LANGUAGE GeneralizedNewtypeDeriving #-}
{-# LANGUAGE MultiParamTypeClasses #-}
{-# LANGUAGE QuasiQuotes #-}
{-# LANGUAGE TemplateHaskell #-}
{-# LANGUAGE TypeFamilies #-}
module Model where

import Data.Text
import Data.Time
import Database.Persist.TH
import Database.Persist.Sql

share
  [mkPersist sqlSettings, mkMigrate &quot;migrateAll&quot;]
  [persistLowerCase|
    Income
      source Text
      amount Double
      when   UTCTime
      deriving Show Eq
    Expense
      towhom Text
      amount Double
      when   UTCTime
      deriving Show Eq
  |]
</code></pre><p>On a second glance, I just realized that we use so many language extensions.
As for the reasons, apart from <code>QuasiQuotes</code> and <code>TemplateHaskell</code>, I don't really
know and just accept that GHC will fail to compile without them.
But if you insist, you can look at the <a href="http://downloads.haskell.org/~ghc/latest/docs/html/users_guide/glasgow_exts.html">docs</a>
about them.</p><p>Anyway, the snippet above, especially the <code>share</code> block, defines that we will
create tables name <code>income</code> and <code>expense</code>, which we have specified before the snippet.</p><p>And for the &quot;migration plan&quot;, just a fancy way to say about creating and/or dropping
tables and/or database before the program talks to it, we will create a function
named <code>doMigration</code> in the same source file.</p><pre><code>-- Model.hs
doMigration :: SqlPersistT IO ()
doMigration = runMigration migrateAll
</code></pre><p>If you wonder where did <code>migrateAll</code> come from, that value comes from the
<code>share</code> block above.
You can see <code>&quot;migrateAll&quot;</code> there. That's the magic of <code>Template Haskell</code>.</p><h4>Read Write</h4><p>Let's start by adding <code>monad-logger</code>, <code>monad-control</code>, and <code>transformers</code> in our
<code>package.yaml</code> with the reason of satisfying constraints of one of our functions.</p><p>We then create a source file named <code>ReadWrite.hs</code> on our <code>src</code> dir.
It will be really messy and we will have a lot of duplicated functions.
For example, a function like this:</p><pre><code>-- ReadWrite.hs
insertIncome :: Text -&gt; Double -&gt; IO (Key Income)
insertIncome source amount = do
  now &lt;- getCurrentTime
  runDb $ insert $ Income source amount now

insertExpense :: Text -&gt; Double -&gt; IO (Key Expense)
insertExpense towhom amount = do
  now &lt;- getCurrentTime
  runDb $ insert $ Expense towhom amount now
</code></pre><p>To be honest, I don't really know how to reduce the duplication.
But that's another homework I have to solve for the next article, I guess.</p><p>Okay, let's import library for <code>ReadWrite.hs</code>.</p><pre><code>-- ReadWrite.hs
import Control.Monad.IO.Class
import Control.Monad.Logger
import Control.Monad.Trans.Control
import Control.Monad.Trans.Reader

import Data.Text
import Data.Time
import Data.Maybe

import Database.Persist.Postgresql (withPostgresqlConn, createPostgresqlPool)
import Database.Esqueleto
</code></pre><p>As you've seen, there are a lot of <code>Control.Monad</code> imports.
Those libraries are used as the wrapper of computed data in our functions.
And the reason is the program we write communicates with <code>RealWorld(tm)</code> and
thus the data from/to our functions is safe and sound. Heheh.</p><p>Plus, don't forget to sprinkle some extensions so GHC can decide what kind of data
we are using in the program.</p><pre><code>-- ReadWrite.hs
{-# LANGUAGE AllowAmbiguousTypes   #-}
{-# LANGUAGE FlexibleContexts      #-}
{-# LANGUAGE GADTs                 #-}
{-# LANGUAGE MultiParamTypeClasses #-}
{-# LANGUAGE OverloadedStrings     #-}
{-# LANGUAGE Rank2Types            #-}
{-# LANGUAGE RankNTypes            #-}
{-# LANGUAGE TypeFamilies          #-}
</code></pre><p>As usual, I really suggest you to read through GHC's documentation to get the
explanations we need (or want) over what those extension do (or don't).</p><p>Finally, we come to the simplest part of the program, read write information
to the database.</p><pre><code>-- ReadWrite.hs
--| For executing queries.
runDb ::
     (MonadIO m, MonadBaseControl IO m)
  =&gt; ReaderT SqlBackend (LoggingT m) a
  -&gt; m a
runDb query = do
  -- Yep, you read that right.
  -- There's password and username hardcoded for db in here.
  let con = &quot;host=localhost port=5432 user=ibnu dbname=bot password=jaran&quot;
  runStderrLoggingT $ withPostgresqlConn con $ \backend -&gt; runReaderT query backend

doingMigration :: (MonadIO m, MonadBaseControl IO m, MonadLogger m) =&gt; m ()
doingMigration = do
  let con = &quot;host=localhost port=5432 user=ibnu dbname=bot password=jaran&quot;
  pool &lt;- createPostgresqlPool con 10
  liftIO $ runSqlPool doMigration pool
</code></pre><p>The function above takes a query, which is a read-only access to the database.
&quot;Read-only&quot; what I mean here is, in a sense, a paved road built by government.
You can't modify it, yet you can freely use it.
Also, we have to use an instance of <code>LoggingT</code> for this function (by appending <code>runStderrLoggingT</code>)
to satisfy the demands from <code>withPostgresqlConn</code> so it can execute the query properly.</p><p>After we wrote the executor, we will write the functions to read and write from/to database.
The snippet about <code>insert</code> above pretty much enough for our need of insert at the moment.
And for querying, we will create a few functions.</p><pre><code>-- ReadWrite.hs
searchIncomeBySource ::
  (MonadBaseControl IO m, MonadIO m) =&gt; Text -&gt; m [Income]
searchIncomeBySource source = do
  incomes &lt;-
    runDb $
    select $
    from $ \inc -&gt; do
      where_ (inc ^. IncomeSource ==. val source)
      limit 10
      orderBy [desc (inc ^. IncomeWhen)]
      return inc
  return $ map entityVal incomes
</code></pre><p>The snippet above queries the database to select from table <code>income</code> which satisfy
the condition (<code>IncomeSource</code> equals to its argument) but only takes 10 rows and
orders it based on <code>when</code> column.</p><p>As I've complained above, about too many function duplications, we can create
a function for expense by replacing 4 token.
Just replace <code>Income</code> to <code>Expense</code> and presto! we have the function we want.</p><p>There's a really rough edge on this file, though.</p><pre><code>totalExpense ::
     ( BaseBackend backend ~ SqlBackend
     , PersistUniqueRead backend
     , PersistQueryRead backend
     , IsPersistBackend backend
     , MonadIO m
     )
  =&gt; ReaderT backend m Double
totalExpense = do
  res &lt;- select $ from $ \exp -&gt; return $ joinV $ sum_ (exp ^. ExpenseAmount)
  return $ head $ map (fromJust . unValue) res

balance :: IO Double
balance = do
  inc &lt;- runDb totalIncome
  exp &lt;- runDb totalExpense
  return $ inc - exp
</code></pre><p>The function <code>totalExpense</code> above disregards three possibilities:</p><ul><li>The table could be empty, thus the <code>res</code> value could be an empty list.</li><li>Because <code>res</code> could be an empty list, the function <code>head</code> is not safe at all.</li><li>And because <code>sum_</code>'s return value is <code>Maybe a</code>, the usage of <code>fromJust</code> is a really bad.</li></ul><p>Or if you love challange, you can modify that function so it returns a default value.
0.0, for example.</p><p>The same goes for many other functions we need. 
You can read it <a href="https://gitlab.com/ibnuda/Telegram-Bot-Walkthrough/blob/feeb49d60a626047f4689072d29a0b3a06a4558f/src/ReadWrite.hs">here</a>
if you want to read the rest.</p><h4>Types and Action</h4><p>If you read the example from <a href="https://github.com/fizruk/telegram-bot-simple">telegram-bot-simple</a>,
you will know that the approach of this library is pretty similar to <a href="https://github.com/dmjio/miso">miso</a>'s
or Elm's approach on state management.
And that means we have to define our <code>model</code> and <code>action</code> where every action on every state (or <code>model</code>)
maybe has a different effects on it.</p><p>So, let's define our <code>model</code> by open <code>Lib.hs</code> file on <code>src</code> dir.</p><pre><code>-- Lib.hs
import Model
import ReadWrite

import Control.Applicative ((&lt;|&gt;))
import Data.Maybe
import Data.Text
import Telegram.Bot.API
import Telegram.Bot.Simple
import Telegram.Bot.Simple.UpdateParser
</code></pre><p>Just standard imports.
Nothing particularly interesting here.</p><pre><code>-- Lib.hs
data ChatState
  = InsertingIncome
  | InsertingIncomeSavedSource Text
  | InsertingExpense
  | InsertingExpenseSavedToWhom Text
  | SearchingIncome
  | SearchingExpense
  | CheckingBalance
  | Other Text
  | EmptyContent
  deriving (Show, Eq)
</code></pre><p>We defined a sumtype named <code>ChatState</code> to model the state of the application.
Because we can't really know the intention of the user when inserting, ah screw it.
Let me show you an example.</p><pre><code>                            You: /income
Bot: Who gave you the money?
                            You: Mom
Bot: How much is it?
                            You: 420
Bot: Okay, saved!
</code></pre><p>Because the nature of the conversation, we can't really rely on the program's &quot;inteligence&quot;.
It's a hard problem in NLP, isn't it?
And we don't even use that.
So, the easiest approach is we hold the state and saved input in the program itself as
part of the <code>state</code>.</p><p>Same goes for the other functions, we only need to take 1 argument tops, if any, before
we call the corresponding query functions.</p><pre><code>-- Lib.hs
data ChatModel =
  ChatModel ChatState
  deriving (Show, Eq)

emptyChatModel :: ChatModel
emptyChatModel = ChatModel EmptyContent

data Action
  = Empty
  | ActHelp
  | ActBalance
  | ActAddInc
  | ActAddExp
  | ActSearchIncome
  | ActSearchExpense
  | ActAddMessage Text
  deriving (Show, Read)
</code></pre><p>On the snippet above, we defined <code>ChatState</code> as, well, the the state of the application
plus its content; or <code>model</code> in Elm's lingo.
We also defined <code>Action</code> that limits what could be done on this program.
And let the <code>Action</code> derives from <code>Show</code> and <code>Read</code> because we need them to be &quot;show-able&quot;
and &quot;read-able&quot;.
Perhaps you're guessing where do we get <code>Action</code> from.
It comes from the parsed or &quot;read&quot; value of message's text (<code>Update</code> data type).
For example, <code>ActHelp</code> is an action that will be used to tell the program to
show the user the &quot;help messages&quot; is a parsed from &quot;/help&quot; command which we will
define in a moment.</p><h4>Inner Part of the Program</h4><p>Next, we will create an &quot;app representation&quot; of our session by adding the following lines.</p><pre><code>-- Lib.hs
incexpBotApp :: BotApp ChatModel Action
incexpBotApp = BotApp
  { botInitialModel = emptyChatModel
  , botAction = flip updateToAction
  , botHandler = updateHandler
  , botJobs = []
  }
</code></pre><p>The defined structure of the <code>BotApp</code> specifies what could be done when given a specific <code>Update</code>.
Oh, forgot to tell, the message we (and bot) send is called <code>Update</code> in this package.</p><p>At the moment, <code>updateToAction</code> and <code>updateHandler</code> haven't been defined yet.
So take a moment to define those functions.</p><pre><code>-- Lib.hs
updateToAction :: ChatModel -&gt; Update -&gt; Maybe Action
updateToAction _ = parseUpdate $
  ActHelp &lt;$ command (pack &quot;help&quot;) &lt;|&gt;
  ActBalance &lt;$ command (pack &quot;balance&quot;) &lt;|&gt;
  ActAddInc &lt;$ command (pack &quot;income&quot;) &lt;|&gt;
  ActAddExp &lt;$ command (pack &quot;expense&quot;) &lt;|&gt;
  ActSearchIncomes &lt;$  command (pack &quot;incomes&quot;) &lt;|&gt;
  ActSearchExpenses &lt;$ command (pack &quot;expense&quot;) &lt;|&gt;
  ActMessage &lt;$&gt; plainText &lt;|&gt;
  callbackQueryDataRead
</code></pre><p>This <code>updateAction</code> function basically takes any kind of chat models and parses
any update it has given to by reading the message's text (<code>Update</code> has a lot of data in it).
If the message's text starts with a slash (<code>/</code>) then it will return the appropriate action
after &quot;choosing&quot; (symbolized by <code>&lt;|&gt;</code> operator) between the <code>pack</code>ed strings.
But when the message's text doesn't start with a slash, the function should regards
the message's text as wrapped <code>Text</code> in <code>ActMessage</code>.</p><p>Next, we will defined <code>updateHandler</code>.
In short, it's a function to modify a state when given an action.
But we will create a helper function first.
The function above will send a <code>String</code> to the other side by transforming the <code>String</code>
to a <code>Text</code> then transform it into <code>ReplyMessage</code> and then send it using <code>reply</code></p><pre><code>-- Lib.hs
replyString :: String -&gt; BotM ()
replyString str = reply . toReplyMessage . pack $ str
</code></pre><p>And then create the function in the following snippets.</p><pre><code>updateHandler :: Action -&gt; Model -&gt; Eff Action ChatModel
updateHandler action model =
  case action of
    Empty -&gt; pure model
</code></pre><p>Whenever a state applied by an <code>Empty</code> action, it will return the state unmodified.
And basically a final destination so we will not receive an endless chat message from
this program.</p><pre><code>    ActHelp -&gt;
      emptyChatModel &lt;# do
        replyString &quot;Help messages&quot;
        pure Empty
</code></pre><p>But when an <code>ActHelp</code> action being applied to any state, we will modify the state
to <code>emptyChatModel</code> and send &quot;Help messages.&quot; to the other side and then returning
an <code>Empty</code> action.</p><p>Oh, and what that <code>&lt;#</code> operator does is, basically, after you send a reply or
do whatever to the other scope of this system, you can return the left side of
this operator.</p><pre><code>    ActBalance -&gt;
      emptyChatModel &lt;# do
        liftIO balance &gt;&gt;= replyString . show
        pure Empty
</code></pre><p>And when <code>ActBalance</code> being applied to any state, we still return an <code>emptyChatModel</code>
and then returning an <code>Empty</code> action after sending a message of the returned value
of <code>balance</code> (the function which returns your balance from database).
About <code>liftIO</code>, you can basically say that it is a magic function which makes
some wrapper into another wrapper.</p><pre><code>    ActAddInc -&gt;
      ChatModel InsertingIncome &lt;# do
        replyString &quot;Who gave you the money?&quot;
        pure Empty
</code></pre><p>When <code>ActAddInc</code> being applied to any state, we will return a <code>ChatModel InsertingIncome</code>
while replying &quot;Who gave you the money?&quot; to the user which followed by returning
an <code>Empty</code> action.</p><p>We will continue the &quot;conversation&quot; in a few minutes.
Please bear with me for a few more cases.</p><pre><code>    ActAddExp -&gt;
      ChatModel InsertingExpense &lt;# do
        replyString &quot;Who did you give it to?&quot;
        pure Empty
</code></pre><p>Basically still the same function as the previous case, actually.</p><pre><code>    ActSearchIncome -&gt;
      ChatModel SearchingIncome &lt;# do
        replyString &quot;Who are you looking for?&quot;
        pure Empty
    ActSearchExpense -&gt;
      ChatModel SearchingExpense &lt;# do
        replyString &quot;Who are you looking for?&quot;
        pure Empty
</code></pre><p>Still the same functions.
But the state indicates the intention that the state itself is used for looking
expenses.</p><pre><code>    ActMessage msg -&gt; messageHandler msg model
</code></pre><p>This case specializes when the message which the user has sent doesn't have a leading
slash.
So we will create a specialized function to pattern-match the state with the message
content.</p><pre><code>messageHandler :: Text -&gt; ChatModel -&gt; Eff Action ChatModel
messageHandler message model =
  case model of
</code></pre><p>To fill the gap between the previous case (<code>ActMessage msg</code>) and the return type
of the function (<code>Eff Action ChatModel</code>), we created a function that takes a <code>Text</code>
(from the <code>msg</code> in previous function) and <code>ChatModel</code> (which passed by the previous function)
and returns an <code>Eff Action ChatModel</code>.</p><p>And we pattern-matched the passed <code>model</code> with the following cases.</p><pre><code>    ChatModel InsertingExpense -&gt;
      ChatModel (InsertingExpenseSavedSource message) &lt;# do
        replyString &quot;How much is it?&quot;
        pure Empty
</code></pre><p>When the model indicates we had an intention to insert an expense before, we will
modify the model to have <code>ChatModel (InsertingExpenseSavedSource message)</code> and then
return it after we reply to the user while returning an <code>Empty</code> action.</p><pre><code>    ChatModel (InsertingExpenseSavedSource source) -&gt;
      case (readMaybe $ unpack message :: Maybe Double) of
        Nothing -&gt; reasking model
        Just amount -&gt;
          ChatModel EmptyContent &lt;# do
            _ &lt;- liftIO $ insertExpense source amount
            replyString &quot;Ok, saved!&quot;
            pure Empty
</code></pre><p>On the other hand, when the program already has <code>ChatModel (InsertingExpenseSavedSource source)</code>
as its state, we will try to read the message as a <code>Double</code>.
If we failed to parse the message, we will call a function to ask the user to re-input
the message.
Otherwise, the parsed input will be used, together with <code>source</code>, as the input values
for <code>insertExpense</code>.
Which followed by sending &quot;Ok, saved!&quot; to the user and returning an <code>Empty</code> action.
All of that will be followed by returning an <code>EmptyContent</code> as the state of the chat.</p><pre><code>    ChatModel InsertingIncome -&gt;
      ChatModel (InsertingExpenseSavedSource message) &lt;# do
        replyString &quot;How much is it?&quot;
        pure Empty
    ChatModel (InsertingIncomeSavedSource source) -&gt;
      case (readMaybe $ unpack message :: Maybe Double) of
        Nothing -&gt; reasking model
        Just amount -&gt;
          ChatModel EmptyContent &lt;# do
            _ &lt;- liftIO $ insertIncome source amount
            replyString &quot;Ok, saved!&quot;
            pure Empty
</code></pre><p>The story that used for the above cases basically the same thing as the previous
explanation.</p><pre><code>    ChatModel SearchingExpense -&gt;
      ChatModel EmptyContent &lt;# do
        expenses &lt;- liftIO $ searchExpenseBySource message
        mapM_ (replyString . show) expenses
        pure Empty
    ChatModel SearchingIncome -&gt;
      ChatModel EmptyContent &lt;# do
        incomes &lt;- liftIO $ searchIncomeBySource message
        mapM_ (replyString . show) incomes
        pure Empty
</code></pre><p>Both the functions above indicate that we had an intention to query the database
about what kind of income/expense from/to a certain entity.
What makes differences here is the <code>mapM_ (replystring . show) expenses</code> (or <code>incomes</code>).
<code>mapM_</code> is a function that iterates a list and then apply each and every element
of the previous list to a function while discards the returned value of the computation.
Pretty neat, ennit? Just like your usual <code>for</code> or <code>while</code>.</p><pre><code>    otherwise -&gt;
      model &lt;# do
        reply . toReplyMessage $ helpMessage
        pure Empty
</code></pre><p>And the rest of pattern matching scheme, we will show the <code>helpMessage</code>.
Like this.</p><pre><code>helpMessage :: Text
helpMessage =
  (intercalate $ pack &quot;\n&quot;) $
  map
    pack
    [ &quot;/help to show this message.&quot;
    , &quot;/balance to show balance.&quot;
    , &quot;/income to insert income.&quot;
    , &quot;/expense to insert expense.&quot;
    , &quot;/incomes to show incomes from a source.&quot;
    , &quot;/expenses to show expenses to an entity.&quot;
    ]</code></pre><h4>Main Function and Telegram Preparation</h4><p>For the main function, we will have a really simple function.</p><pre><code>-- Lib.hs
someFunc :: IO ()
someFunc = do
  runStderrLoggingT doingMigration
  token &lt;- Token . pack &lt;$&gt; getEnv &quot;TOKEN_TELEGRAM&quot;
  env &lt;- defaultTelegramClientEnv token
  startBot_ (conversationBot updateChatId incexpBotApp) env
</code></pre><p>Which basically creating tables, reading an env. variable, and then start the bot.</p><p>And now, we will create a new token for our bot.
I will not screenshot the conversation between my account and @BotFather.</p><pre><code>                          me: /newbot
BotFather: What's the name?
                          me: SomethingBot
BotFather: Ok. Here's your token. $TOKEN_TELEGRAM
</code></pre><h4>Deployment</h4><p>All right, now you've got your token.
Now, you have to set an public IP facing web server plus tls connection thingy.
I've already set one before using <a href="2018-02-01-ssl-on-ip-address.html">my own</a> guide.</p><p>Let's try to save a few records.</p><pre><code>                                      Ibnu Aji: /income
Expendable_Bot: Who gave you the money?
                                      Ibnu Aji: Mom
Expendable_Bot: How much is it?
                                      Ibnu Aji: 420
Expendable_Bot: Ok, saved!
                                      Ibnu Aji: /expense
Expendable_Bot: Who did you give it to?
                                      Ibnu Aji: Little Sister
Expendable_Bot: How much is it?
                                      Ibnu Aji: 100
Expendable_Bot: Ok, saved!
                                      Ibnu Aji: /balance
Expendable_Bot: 320.0
</code></pre><p>Okay, we're finished here.
It was a nice experience for me to show one of my friend about how easy it is
to create a telegram bot.</p><h4>Exercises, If You Want</h4><p>That bot assumes only one account who uses it.
You can see that in the table as there's no <code>account</code> or anything that can
be recognized as one.
So, if you want to flex your finger a bit, you can.</p><ol><li>Default value in <code>balance</code>.</li><li>User management.</li></ol>
            </section>
        </main>
    </body>
    <footer>
        This material is shared under the 
        <a href="https://creativecommons.org/licenses/by/4.0">
            CC-BY License.
        </a>
    </footer>
</html>
